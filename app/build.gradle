import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

ext.supportLibs_version = '28.0.0'
ext.bearsdk_version = '3.0.0'

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.bear2b.sampleapp"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode getTimeStamp()
        versionName bearsdk_version + "-" + getTimeStamp()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }

    kapt {
        generateStubs = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "lang"
    productFlavors {
        java {
            dimension "lang"
        }
        kotlin {
            dimension "lang"
            versionNameSuffix "-kt"
            applicationIdSuffix ".kt"
        }
    }

    splits {
        abi {
            enable true
            reset()
            include "armeabi-v7a", "arm64-v8a"
            universalApk false
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'inputList.txt'
        exclude 'LICENSE'
        exclude 'LICENSE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'asm-license.txt'
        exclude 'NOTICE'
        exclude 'mockito-extensions/org.mockito.plugins.MockMaker'
    }
}

configurations.all {
    // check for updates every build
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {
    implementation("com.bear:bearsdk:$bearsdk_version@aar") {
        transitive = true
    }

    // FireBase
    implementation "com.google.firebase:firebase-messaging:18.0.0"

    // Support
    implementation "com.android.support:support-annotations:$supportLibs_version"
    implementation "com.android.support:appcompat-v7:$supportLibs_version"
    implementation "com.android.support:support-v13:$supportLibs_version"
    implementation "com.android.support:design:$supportLibs_version"
    implementation "com.android.support:cardview-v7:$supportLibs_version"

    // Runtime Permissions
    implementation 'com.github.hotchemi:permissionsdispatcher:2.4.0'
    annotationProcessor 'com.github.hotchemi:permissionsdispatcher-processor:2.4.0'
    kapt 'com.github.hotchemi:permissionsdispatcher-processor:2.4.0'

    implementation 'com.android.support:multidex:1.0.3'

    kotlinImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

static def getTimeStamp() {
    def df = new SimpleDateFormat("yyMMddHHmm")
    return df.format(new Date()).toInteger()
}

apply plugin: 'com.google.gms.google-services'
